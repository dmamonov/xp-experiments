import native iconv.*;
import native standard.implode;
import native standard.str_split;
import native standard.var_dump;
import static util.cmd.Console.writeLine;

public class StringBuffer {
  protected string $buffer;
  protected int $_length;

  public __construct(string $initial= '') {
    $this.buffer= $initial;
    $this._length= iconv_strlen($initial, 'utf-8');
  }
  
  public self append(string $str) {
    $this.buffer~= $str;
    $this._length+= iconv_strlen($str, 'utf-8');
    return $this;
  }
  
  public static self valueOf(string[] $in) {
    return new self(implode(' ', $in));
  }
  
  public string this[int $offset] {
    get {
      return iconv_substr($this.buffer, $offset, 1, 'utf-8');
    }
    set {
      throw new lang.IllegalAccessException('Cannot write to string offsets');
    }
    unset {
      throw new lang.IllegalAccessException('Cannot remove string offsets');
    }
    isset {
      return $offset >= 0 && $offset < $this._length;
    }
  }
  
  public int length {
    get { return $this._length; }
    set { throw new lang.IllegalAccessException('Cannot set string length'); }
  }

  public string[] chars {
    get { return str_split($this.buffer); }
  }

  public string toString() {
    return $this.buffer;
  }
  
  public static void main(string[] $args) {
    $s= self::valueOf($args).append('!');
    writeLine('String= "', $s, '" (length= ', $s.length, ') [0]= "', $s[0], '"');
    
    try {
      $s.length= 10;
    } catch (lang.IllegalAccessException $e) {
      writeLine('Caught expected ', $e.compoundMessage());
    }

    writeLine('chars= ', $s.chars);
  }  
}
