package demo;

import native iconv.*;
import native standard.implode;

public class StringBuffer {
  protected string $buffer;
  public int $length;

  public __construct(string $initial= '') {
    $this.buffer= $initial;
    $this.length= iconv_strlen($initial, 'utf-8');
  }
  
  public self append(string $str) {
    $this.buffer~= $str;
    $this.length+= iconv_strlen($str, 'utf-8');
    return $this;
  }
  
  public static self valueOf(string[] $in) {
    return new self(implode(' ', $in));
  }
  
  public string this[int $offset] {
    get {
      return iconv_substr($this.buffer, $offset, 1, 'utf-8');
    }
    set {
      throw new lang.IllegalAccessException('Cannot write to string offsets');
    }
    unset {
      throw new lang.IllegalAccessException('Cannot remove string offsets');
    }
    isset {
      return $offset >= 0 && $offset < $this.length;
    }
  }

  public string toString() {
    return $this.buffer;
  }
  
  public static void main(string[] $args) {
    $s= self::valueOf($args).append('!');
    util.cmd.Console::writeLine('String= "', $s, '" (length= ', $s.length, ') [0]= "', $s[0], '"');
  }  
}
